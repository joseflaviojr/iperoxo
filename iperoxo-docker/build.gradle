//------------------------------------------------------

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"

//------------------------------------------------------

String nome = "Nome"
String descricao = "Descrição da aplicação."

group = "br.com.grupo"
archivesBaseName = "nome"
version = "1.0"

//------------------------------------------------------

[

"biblioteca", // Dependências diretas: *.jar
"fonte",      // Código fonte: *.java
"recurso",    // Anexos que serão embutidos no ".jar"
"dist",       // Anexos da distribuição
"projeto",    // Arquivos de projeto: doc, svg, psd, etc.
"rascunho"    // Arquivos temporários e descartáveis

].each { new File(it).mkdirs() }

//------------------------------------------------------

float javaVersao = 1.8
String codificacao = "UTF-8"

sourceCompatibility = javaVersao

tasks.withType(JavaCompile) {
    options.encoding = codificacao
}

tasks.withType(GroovyCompile) {
    options.encoding = codificacao
    groovyOptions.encoding = codificacao
}

sourceSets {
    main.java.srcDirs = ["fonte"]
    main.resources.srcDirs = ["recurso"]
}

//------------------------------------------------------

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir:"biblioteca", include:["*.jar"])
    compile "org.apache.tomcat:tomcat-catalina:8.5.41"
    compile "org.apache.geronimo.specs:geronimo-osgi-locator:1.1"
    compile "org.eclipse.persistence:eclipselink:2.7.4"
    compile "org.postgresql:postgresql:42.2.5"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:2.11.2"
    compile "com.joseflavio:iperoxo:1.0-A19"
}

//------------------------------------------------------

jar {
    manifest {
        attributes "Implementation-Title": nome,
                   "Implementation-Version": version,
                   "Main-Class": "com.joseflavio.iperoxo.IpeRoxo",
                   "Class-Path": configurations.compile.collect{ it.getName() }.join(" ")
    }
}

//------------------------------------------------------

task distDep(type: Copy, dependsOn: [jar]) {
    into "$buildDir/dist"
    from "$projectDir/dist"
    from "$buildDir/libs/" + archivesBaseName + "-" + version + ".jar"
    from configurations.compile
}

task distExe {
    doLast {
        def destino = new File("build/exe")
        destino.mkdirs()
        def comando = new File("build/exe/iperoxo.sh")
        def nomeJar = archivesBaseName + "-" + version + ".jar"
        comando << "#!/bin/sh\n\n"
        comando << "java -server -XX:+TieredCompilation -XX:+UseBiasedLocking "
        comando << "-jar $nomeJar \"\$@\"\n"
    }
}

task dist(type: Copy, dependsOn: [distDep, distExe]) {
    into "$buildDir/dist"
    from "$buildDir/exe"
    fileMode 0755
}

build.dependsOn dist

//------------------------------------------------------

eclipse {
    project {
        name = archivesBaseName
        comment = descricao
    }
    jdt {
        sourceCompatibility = javaVersao
        targetCompatibility = javaVersao
    }
}

eclipseJdt {
    doLast {
        file(".settings/org.eclipse.core.resources.prefs").text =
            "eclipse.preferences.version=1\nencoding/<project>=" + codificacao
    }
}

//------------------------------------------------------

idea {
    project {
        vcs = "Git"
        ipr {
            withXml {
                def projeto = it.asNode()
                def encoding = projeto.component.find { it.@name == "Encoding" }
                encoding.appendNode("file", [url: "PROJECT", charset: codificacao])
            }
        }
    }
    module {
        languageLevel = new org.gradle.plugins.ide.idea.model.IdeaLanguageLevel(javaVersao)
        jdkName = "" + javaVersao
    }
}

//------------------------------------------------------