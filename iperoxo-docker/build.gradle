//------------------------------------------------------

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
}

//------------------------------------------------------

String nome      = 'Nome'
String descricao = 'Descrição da aplicação.'

group   = 'br.com.grupo'
version = '1.0'

//------------------------------------------------------

float  javaVersao  = 1.8
String codificacao = 'UTF-8'

sourceCompatibility = javaVersao
targetCompatibility = javaVersao

tasks.withType(JavaCompile) {
    options.encoding = codificacao
}

//------------------------------------------------------

repositories {
    mavenCentral()
}

//------------------------------------------------------

dependencies {
    implementation fileTree(dir:'lib', include:['*.jar'])
    implementation 'org.apache.tomcat:tomcat-catalina:8.5.51'
    implementation 'org.apache.geronimo.specs:geronimo-osgi-locator:1.1'
    implementation 'org.eclipse.persistence:eclipselink:2.7.6'
    implementation 'org.postgresql:postgresql:42.2.10'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.0'
    implementation 'com.joseflavio:iperoxo:1.0-A22'
    testImplementation 'junit:junit:4.12'
}

//------------------------------------------------------

jar {
    manifest {
        attributes 'Implementation-Title': nome,
                   'Implementation-Version': archiveVersion,
                   'Main-Class': 'com.joseflavio.iperoxo.IpeRoxo',
                   'Class-Path': configurations.runtimeClasspath.collect{ it.getName() }.join(' ')
    }
}

//------------------------------------------------------

java {
    withSourcesJar()
    withJavadocJar()
}

//------------------------------------------------------

javadoc {
    if( JavaVersion.current().isJava9Compatible() ){
        options.addBooleanOption('html5', true)
    }
}

//------------------------------------------------------

task copiarDeps(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/deps"
}

//------------------------------------------------------

task distDep(type: Copy, dependsOn: [jar]) {
    from configurations.runtimeClasspath
    from "$projectDir/dist"
    from "$buildDir/libs/" + rootProject.name + "-" + version + ".jar"
    into "$buildDir/dist"
}

task distExe {
    doLast {
        def destino = new File("build/exe")
        destino.mkdirs()
        def comando = new File("build/exe/iperoxo.sh")
        def nomeJar = rootProject.name + "-" + version + ".jar"
        comando << "#!/bin/sh\n\n"
        comando << "java -server -XX:+TieredCompilation -XX:+UseBiasedLocking "
        comando << "-jar $nomeJar \"\$@\"\n"
    }
}

task dist(type: Copy, dependsOn: [distDep, distExe]) {
    from "$buildDir/exe"
    into "$buildDir/dist"
    fileMode 0755
}

build.dependsOn dist

//------------------------------------------------------

eclipse {
    project {
        name    = rootProject.name
        comment = descricao
    }
    jdt {
        sourceCompatibility = javaVersao
        targetCompatibility = javaVersao
    }
}

eclipseJdt {
    doLast {
        file('.settings/org.eclipse.core.resources.prefs').text =
            'eclipse.preferences.version=1\nencoding/<project>=' + codificacao
    }
}

//------------------------------------------------------

idea {
    project {
        languageLevel = new org.gradle.plugins.ide.idea.model.IdeaLanguageLevel(javaVersao)
        jdkName = '' + javaVersao
        vcs = 'Git'
        ipr {
            withXml {
                def projeto  = it.asNode()
                def encoding = projeto.component.find { it.@name == 'Encoding' }
                encoding.appendNode('file', [url: 'PROJECT', charset: codificacao])
            }
        }
    }
}

//------------------------------------------------------