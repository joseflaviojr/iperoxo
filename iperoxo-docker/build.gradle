//------------------------------------------------------

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
}

//------------------------------------------------------

String nome      = 'Nome'
String descricao = 'Descrição da aplicação.'

group   = 'br.com.grupo'
version = '1.0'

//------------------------------------------------------

float  javaVersao  = 1.8
String codificacao = 'UTF-8'

sourceCompatibility = javaVersao
targetCompatibility = javaVersao

tasks.withType(JavaCompile) {
    options.encoding = codificacao
}

//------------------------------------------------------

repositories {
    mavenCentral()
}

//------------------------------------------------------

dependencies {
    implementation fileTree(dir:'lib', include:['*.jar'])
    implementation 'org.apache.tomcat:tomcat-catalina:8.5.63'
    implementation 'org.apache.geronimo.specs:geronimo-osgi-locator:1.1'
    implementation 'org.eclipse.persistence:eclipselink:2.7.8'
    implementation 'org.postgresql:postgresql:42.2.19'
    implementation 'mysql:mysql-connector-java:8.0.23'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.0'
    implementation 'com.joseflavio:iperoxo:1.0-A23'
    testImplementation 'junit:junit:4.12'
}

//------------------------------------------------------

jar {
    manifest {
        attributes 'Implementation-Title': nome,
                   'Implementation-Version': archiveVersion,
                   'Main-Class': 'com.joseflavio.iperoxo.IpeRoxo',
                   'Class-Path': configurations.runtimeClasspath.collect{ 'lib/' + it.getName() }.join(' ')
    }
}

//------------------------------------------------------

java {
    withSourcesJar()
    withJavadocJar()
}

//------------------------------------------------------

javadoc {
    if( JavaVersion.current().isJava9Compatible() ){
        options.addBooleanOption('html5', true)
    }
}

//------------------------------------------------------

task distConf(type: Copy) {
    from "$projectDir/src/main/resources/Configuracao.properties"
    into "$buildDir/dist"
}

task distProj(type: Copy, dependsOn: [jar]) {
    from "$projectDir/dist"
    from "$buildDir/libs/" + rootProject.name + "-" + version + ".jar"
    into "$buildDir/dist"
}

task distLib(type: Copy, dependsOn: [jar]) {
    from configurations.runtimeClasspath
    into "$buildDir/dist/lib"
}

task distSh {
    doLast {
        def destino = new File("$buildDir/exe")
        destino.mkdirs()
        def nomeJar = rootProject.name + "-" + version + ".jar"
        new File("$buildDir/exe","iperoxo.sh").withWriter("utf-8") { o ->
            o.write "#!/bin/sh\n\n"
            o.write "java -server -XX:+TieredCompilation -jar $nomeJar \"\$@\"\n"
        }
    }
}

task distSh2 {
    doLast {
        def destino = new File("$buildDir/exe")
        destino.mkdirs()
        def nomeJar = rootProject.name + "-" + version + ".jar"
        new File("$buildDir/exe", rootProject.name + ".sh").withWriter("utf-8") { o ->
            o.write "#!/bin/sh\n\n"
            o.write "java -server -XX:+TieredCompilation -jar $nomeJar Configuracao.properties\n"
        }
    }
}

task distBat {
    doLast {
        def destino = new File("$buildDir/exe")
        destino.mkdirs()
        def nomeJar = rootProject.name + "-" + version + ".jar"
        new File("$buildDir/exe","iperoxo.bat").withWriter("utf-8") { o ->
            o.write "@echo off\r\n\r\n"
            o.write "java -server -XX:+TieredCompilation -jar $nomeJar %*\r\n"
        }
    }
}

task distBat2 {
    doLast {
        def destino = new File("$buildDir/exe")
        destino.mkdirs()
        def nomeJar = rootProject.name + "-" + version + ".jar"
        new File("$buildDir/exe", rootProject.name + ".bat").withWriter("utf-8") { o ->
            o.write "@echo off\r\n\r\n"
            o.write "java -server -XX:+TieredCompilation -jar $nomeJar Configuracao.properties\r\n"
        }
    }
}

task dist(type: Copy, dependsOn: [distConf, distProj, distLib, distSh, distSh2, distBat, distBat2]) {
    from "$buildDir/exe"
    into "$buildDir/dist"
    fileMode 0755
}

build.dependsOn dist

//------------------------------------------------------

eclipse {
    project {
        name    = rootProject.name
        comment = descricao
    }
    jdt {
        sourceCompatibility = javaVersao
        targetCompatibility = javaVersao
    }
}

eclipseJdt {
    doLast {
        file('.settings/org.eclipse.core.resources.prefs').text =
            'eclipse.preferences.version=1\nencoding/<project>=' + codificacao
    }
}

//------------------------------------------------------

idea {
    project {
        languageLevel = new org.gradle.plugins.ide.idea.model.IdeaLanguageLevel(javaVersao)
        jdkName = '' + javaVersao
        vcs = 'Git'
        ipr {
            withXml {
                def projeto  = it.asNode()
                def encoding = projeto.component.find { it.@name == 'Encoding' }
                encoding.appendNode('file', [url: 'PROJECT', charset: codificacao])
            }
        }
    }
}

//------------------------------------------------------